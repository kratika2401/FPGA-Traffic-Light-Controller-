module debounce_sync
#(
    parameter integer TICKS = 5
)
(
    input  wire clk,
    input  wire rst_n,
    input  wire tick,

    input  wire async_in,

    output reg  pulse,
    output reg  level
);

    // 2-FF Synchronizer

    reg s1;
    reg s2;

    always @(posedge clk)
    begin
        s1 <= async_in;
        s2 <= s1;
    end

    // Debounce Logic

    reg [$clog2(TICKS+1)-1:0] db_count;
    reg stable;

    always @(posedge clk or negedge rst_n)
    begin
        if(!rst_n)
        begin
            db_count <= 0;
            stable   <= 0;
            level    <= 0;
            pulse    <= 0;
        end
        else
        begin
            pulse <= 0;

            if(tick)
            begin
                if(s2 != stable)
                begin
                    db_count <= db_count + 1;

                    if(db_count == TICKS)
                    begin
                        stable   <= s2;
                        db_count <= 0;
                    end
                end
                else
                begin
                    db_count <= 0;
                end

                if(stable && !level)
                begin
                    level <= 1;
                    pulse <= 1;
                end
                else if(!stable)
                begin
                    level <= 0;
                end
            end
        end
    end

endmodule